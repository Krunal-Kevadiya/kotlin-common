apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: '../settings/codequality/quality.gradle'

apply plugin: 'com.github.dcendents.android-maven'
group='com.kevadiyakrunalk'

android {
    compileSdkVersion build_versions.target_sdk

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += 'src/main/generated'

        androidTest.java.srcDirs += 'src/test/kotlin'
    }

    androidExtensions {
        experimental = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguard_list.each { pro_guard ->
                proguardFile pro_guard
            }
        }

        debug {
            minifyEnabled false
            proguard_list.each { pro_guard ->
                proguardFile pro_guard
            }
        }
    }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    // Support Design
    compileOnly deps.androidx.appcompat
    compileOnly deps.androidx.material
    compileOnly deps.androidx.recyclerview

    // Kotlin
    compileOnly deps.kotlin.stdlib
    compileOnly deps.kotlin.coroutines_core
    //compileOnly deps.kotlin.reflect

    // Location
    compileOnly deps.google.location

    // Socket IO
    compileOnly deps.thirdParty.socket_io

    // GSON
    compileOnly deps.thirdParty.gson

    // Retrofit
    compileOnly(deps.retrofit.lib, {
        exclude group: 'com.squareup.okhttp3'
    })
    compileOnly(deps.retrofit.adapter_rx2, {
        exclude group: 'com.squareup.retrofit'
        exclude group: 'io.reactivex.rxjava2'
    })
    compileOnly(deps.retrofit.gson, {
        exclude group: 'com.squareup.retrofit'
    })
    compileOnly deps.okhttp.lib
    compileOnly(deps.okhttp.logging, {
        exclude group: 'com.squareup.okhttp'
    })

    // Reactive
    compileOnly(deps.javaxAndRx.android2, {
        exclude group: 'io.reactivex', module: 'rxjava2'
    })
    compileOnly deps.javaxAndRx.java2

    //Livedata
    compileOnly deps.lifecycle.livedata
}

// For jitPack
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

/*
// For Bintry or jcenter
ext {
    PUBLISH_GROUP_ID = 'com.kevadiyakrunalk'
    PUBLISH_ARTIFACT_ID = 'kotlin-common'
    PUBLISH_VERSION = '1.0.0'
}

apply from: '../settings/maven/android-release-aar.gradle'*/
